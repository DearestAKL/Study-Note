### 立方体反射-上

首先创建一个CubeMap

![image-20201208165032731](C:\Users\Administrator\AppData\Roaming\Typora\typora-user-images\image-20201208165032731.png)

然后编辑CubeMap

![image-20201208165102927](C:\Users\Administrator\AppData\Roaming\Typora\typora-user-images\image-20201208165102927.png)

编写Shader程序：

```c++
Shader "Custom/Reflection/Base" {
	Properties {
		_MainTex ("Albedo (RGB)", 2D) = "white" {}
		_MainTint ("Main Tint", Color) = (1,1,1,1)
		_CubeMap ("CubeMap", CUBE) = "" {}
		_ReflAmount ("Reflection Amount", Range(0.01, 1)) = 0.5
	}
	SubShader {
		Tags { "RenderType"="Opaque"}
		LOD 200
		
		CGPROGRAM
		#pragma surface surf Lambert
 
		sampler2D _MainTex;
		fixed4 _MainTint;
		samplerCUBE _CubeMap;
		fixed _ReflAmount;
 
		struct Input {
			float2 uv_MainTex;
			float3 worldRefl;
		};
 
		void surf (Input IN, inout SurfaceOutput o) {
			fixed4 c = tex2D (_MainTex, IN.uv_MainTex) * _MainTint;
			o.Emission = texCUBE(_CubeMap, IN.worldRefl).rgb * _ReflAmount;
			o.Albedo = c.rgb;
			o.Alpha = c.a;
		}
 
		ENDCG
	}
	FallBack "Diffuse"
}
```

1、Input里面增加了worldRefl变量，表示世界空间的反射向量。如图所示：

![image-20201208165424574](C:\Users\Administrator\AppData\Roaming\Typora\typora-user-images\image-20201208165424574.png)

worldRefl = viewDir - 2.0 * normal * dot(viewDir, normal)

2、使用texCUBE获取worldRefl位置上的Cubemap上的色彩值，然后乘以系数传递给o.Emission（自发光）。

将shader和Cubemap应用到场景物体上就会获得这样的效果：

![image-20201208165459735](C:\Users\Administrator\AppData\Roaming\Typora\typora-user-images\image-20201208165459735.png)

接下来为反射增加遮罩。

首先在Properties里添加：

```c++
_ReflMask ("Reflection Mask", 2D) = "" {}
```

SubShader里添加：

```c++
sampler2D _ReflMask;
```

修改surf方法：

```c++
void surf (Input IN, inout SurfaceOutput o) {
    fixed4 c = tex2D (_MainTex, IN.uv_MainTex) * _MainTint;
    fixed3 r = texCUBE(_CubeMap, IN.worldRefl).rgb;
    fixed4 m = tex2D (_ReflMask, IN.uv_MainTex);

    o.Emission = (r * m.r) * _ReflAmount;

    o.Albedo = c.rgb;
    o.Alpha = c.a;
}
```

修改很少，只是获取了mask里面的色彩值，与Cubemap上的色彩值相乘。

遮罩：

![image-20201208165941567](C:\Users\Administrator\AppData\Roaming\Typora\typora-user-images\image-20201208165941567.png)

得到效果：

![image-20201208165914523](C:\Users\Administrator\AppData\Roaming\Typora\typora-user-images\image-20201208165914523.png)